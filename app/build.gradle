apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.facom.facomemfoco"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "API_ENDPOINT", "\"$rootProject.ext.apiRelease\""
            buildConfigField "String", "API_VERSION", "\"$rootProject.ext.apiVersion\""
            resValue "string", "app_name", rootProject.ext.appName
            resValue "bool", "enable_crashlytics", "true"
        }

        staging {
            initWith(buildTypes.release)
            applicationIdSuffix ".staging"
            buildConfigField "String", "API_ENDPOINT", "\"$rootProject.ext.apiStaging\""
            buildConfigField "String", "API_VERSION", "\"$rootProject.ext.apiVersion\""
            resValue "string", "app_name", "\"$rootProject.ext.appName [staging]\""
            resValue "bool", "enable_crashlytics", "true"
        }

        debug {
            applicationIdSuffix ".debug"
            buildConfigField "String", "API_ENDPOINT", "\"$rootProject.ext.apiStaging\""
            buildConfigField "String", "API_VERSION", "\"$rootProject.ext.apiVersion\""
            resValue "string", "app_name", "\"$rootProject.ext.appName [debug]\""
            resValue "bool", "enable_crashlytics", "false"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // android support
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    api 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'

    //rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'

    // room
    kapt 'androidx.room:room-compiler:2.1.0'
    api 'androidx.room:room-runtime:2.1.0'
    implementation 'androidx.room:room-rxjava2:2.1.0'

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.ext.kotlinVersion"

    // Kotlin Reflection
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.41"

    // multidex
    implementation 'androidx.multidex:multidex:2.0.1'

    // glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // LiveData + ViewModel
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    //Shimmer
    implementation 'com.facebook.shimmer:shimmer:0.4.0'

    // easy image
    implementation 'com.github.jkwiecien:EasyImage:2.0.3'

    //rx permissions
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    //Stetho
    implementation 'com.facebook.stetho:stetho:1.5.1'
}
